buildscript {
  repositories { jcenter() }

  dependencies {
    classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:3.0.3'
  }
}

group 'io.sczyh30'
version '1.0'

configure(allprojects) { project ->

  apply plugin: 'java'
  apply plugin: 'nebula.optional-base'

  ext {
    vertxVersion = "3.4.1"
  }

  repositories {
    jcenter()
  }

  dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
  }

  compileJava {
    targetCompatibility = 1.8
    sourceCompatibility = 1.8
  }
}

project("pim-common-infrastructure") {
  dependencies {
    compile("io.vertx:vertx-core:${vertxVersion}")
    compile("io.vertx:vertx-web:${vertxVersion}", optional)
    compile("io.vertx:vertx-rx-java:${vertxVersion}", optional)
  }
}

project("pim-http-server") {

  dependencies {
    compile(project(":pim-common-infrastructure"))
    compile("io.vertx:vertx-web:${vertxVersion}")
    compile("io.vertx:vertx-mongo-client:${vertxVersion}")
    compile("io.vertx:vertx-rx-java:${vertxVersion}")
  }

  jar {
    archiveName = 'pim-http-server.jar'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    manifest {
      attributes 'Main-Class': 'io.vertx.core.Launcher'
      attributes 'Main-Verticle': 'io.sczyh30.pim.server.PimRestVerticle'
    }
  }
}

project("pim-rpc-client") {

  dependencies {
    compile(project(":pim-common-infrastructure"))
    compile("io.vertx:vertx-web-client:${vertxVersion}", optional)
    compile("io.vertx:vertx-rx-java:${vertxVersion}", optional)
  }
}

project("pim-gui") {

  dependencies {
    compile(project(":pim-common-infrastructure"))
    compile(project(":pim-rpc-client"))
    compile("io.reactivex:rxjava:1.3.0")
  }

  jar {
    archiveName = 'pim-gui.jar'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    manifest {
      attributes 'Main-Class': 'io.sczyh30.pim.gui.PimGuiApplication'
    }
  }
}
